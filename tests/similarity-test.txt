const std = @import("std");
const basic_block_ = @import("basic-block.zig");
const ir_ = @import("ir.zig");
const lval_ = @import("lval.zig");
const span_ = @import("span.zig");
const symbol_ = @import("symbol.zig");

pub const CFG = struct {
    ir_head: ?*ir_.IR,
    ir_tail: ?*ir_.IR,

    block_graph_head: ?*basic_block_.Basic_Block,

    basic_blocks: std.ArrayList(*basic_block_.Basic_Block),

    children: std.ArrayList(*CFG),

    parameters: std.ArrayList(*lval_.Symbol_Version),

    symbvers: std.ArrayList(*lval_.Symbol_Version),

    symbol: *symbol_.Symbol,

    number_temps: usize,

    return_symbol: *symbol_.Symbol,

    interned_strings: *std.StringArrayHashMap(usize),

    visited: bool,

    offset: ?i64,
    locals_size: ?i64,

    allocator: std.mem.Allocator,

    pub fn init(symbol: *symbol_.Symbol, caller: ?*CFG, interned_strings: *std.StringArrayHashMap(usize), allocator: std.mem.Allocator) !*CFG {
        if (symbol.cfg) |cfg| {
            return cfg;
        }
        var retval = allocator.create(CFG) catch unreachable;
        retval.ir_head = null;
        retval.ir_tail = null;
        retval.block_graph_head = null;
        retval.basic_blocks = std.ArrayList(*basic_block_.Basic_Block).init(allocator);
        retval.children = std.ArrayList(*CFG).init(allocator);
        retval.symbvers = std.ArrayList(*lval_.Symbol_Version).init(allocator);
        retval.parameters = std.ArrayList(*lval_.Symbol_Version).init(allocator);
        retval.symbol = symbol;
        retval.number_temps = 0;
        retval.return_symbol = symbol_.Symbol.init(symbol.scope, "$retval", span_.Span{ .filename = "", .line_text = "", .col = 0, .line = 0 }, symbol._type.rhs(), undefined, null, .mut, allocator);
        retval.return_symbol.expanded_type = retval.return_symbol._type.expand_type(allocator);
        retval.visited = false;
        retval.interned_strings = interned_strings;
        retval.offset = null;
        retval.locals_size = null;
        retval.allocator = allocator;
        symbol.cfg = retval;

        if (caller) |caller_node| {
            caller_node.children.append(retval) catch unreachable;
        }

        return retval;
    }

    pub fn deinit(self: *CFG) void {
        for (self.basic_blocks.items) |bb| {
            bb.deinit();
        }
        self.basic_blocks.deinit();

        self.symbvers.deinit();

        for (self.parameters.items) |param| {
            param.deinit();
        }
        self.parameters.deinit();

        self.allocator.destroy(self);
    }

    pub fn clearVisitedBBs(self: *CFG) void {
        for (self.basic_blocks.items) |bb| {
            bb.visited = false;
        }
    }

    pub fn clearVisited(self: *CFG) void {
        self.visited = false;
        for (self.children.items) |child| {
            if (child.visited) {
                child.clearVisited();
            }
        }
    }

    pub fn collect_generated_symbvers(self: *CFG) void {
        for (self.basic_blocks.items) |bb| {
            var maybe_ir = bb.ir_head;
            while (maybe_ir) |ir| : (maybe_ir = ir.next) {
                if (ir.dest != null and
                    ir.dest.?.* == .symbver and
                    ir.dest.?.symbver.findSymbolVersionSet(&self.parameters) == null)
                {
                    _ = ir.dest.?.symbver.putSymbolVersionSet(&self.symbvers);
                }
            }
        }
    }

    pub fn appendInstruction(self: *CFG, ir: *ir_.IR) void {
        if (self.ir_head == null) {
            self.ir_head = ir;
        } else if (self.ir_tail == null) {
            self.ir_head.?.next = ir;
            self.ir_tail = ir;
            self.ir_tail.?.prev = self.ir_head;
        } else {
            self.ir_tail.?.next = ir;
            ir.prev = self.ir_tail;
            self.ir_tail = ir;
        }
    }

    pub fn basicBlockFromIR(self: *CFG, maybe_ir: ?*ir_.IR, allocator: std.mem.Allocator) ?*basic_block_.Basic_Block {
        if (maybe_ir == null) {
            return null;
        } else if (maybe_ir.?.in_block) |in_block| {
            return in_block;
        }
        var retval: *basic_block_.Basic_Block = basic_block_.Basic_Block.init(allocator);
        self.basic_blocks.append(retval) catch unreachable;
        retval.ir_head = maybe_ir;
        var _maybe_ir = maybe_ir;
        while (_maybe_ir) |ir| : (_maybe_ir = ir.next) {
            ir.in_block = retval;

            if (ir.dest != null and ir.dest.?.* == .symbver) {
                ir.dest.?.symbver.makeUnique();
            }

            if (ir.kind == .label) {
                retval.has_branch = false;
                retval.next = self.basicBlockFromIR(ir.next, allocator);
            } else if (ir.kind == .jump) {
                retval.has_branch = false;
                if (ir.data == .branch) {
                    if (ir.data.branch) |branch| {
                        retval.next = self.basicBlockFromIR(branch.next, allocator);
                    } else {
                        retval.next = self.basicBlockFromIR(null, allocator);
                    }
                } else {
                    retval.next = null;
                }
            } else if (ir.kind == .panic) {
                retval.has_branch = false;
                retval.next = null;
                retval.has_panic = true;
                if (ir.next != null and ir.next.?.next != null) {
                    ir.next.?.next.?.prev = null;
                    ir.next.?.next = null;
                }
                break;
            } else if (ir.kind == .branchIfFalse) {
                retval.has_branch = true;
                var branchNext: ?*ir_.IR = null;
                if (ir.data.branch) |branch| {
                    branchNext = branch.next;
                } else {
                    branchNext = null;
                }
                retval.next = self.basicBlockFromIR(ir.next, allocator);
                retval.branch = self.basicBlockFromIR(branchNext, allocator);
                retval.condition = ir.src1;
            }
        }
        return retval;
    }

    pub fn removeBasicBlockLastInstruction(cfg: *CFG) void {
        for (cfg.basic_blocks.items) |bb| {
            if (bb.ir_head == null) {
                continue;
            } else if (bb.ir_head.?.next == null) {
                bb.ir_head = null;
            } else {
                var maybe_ir: ?*ir_.IR = bb.ir_head;
                while (maybe_ir.?.next != null) : (maybe_ir = maybe_ir.?.next) {}
                maybe_ir.?.prev.?.next = null;
            }
        }
    }

    pub fn calculatePhiParamsAndArgs(self: *CFG, allocator: std.mem.Allocator) void {
        for (self.basic_blocks.items) |bb| {
            bb.parameters.clearRetainingCapacity();
            bb.next_arguments.clearRetainingCapacity();
            bb.branch_arguments.clearRetainingCapacity();

            var maybe_ir: ?*ir_.IR = bb.ir_head;
            //
            while (maybe_ir) |ir| : (maybe_ir = ir.next) { //
                if (ir.dest != null and ir.dest.?.* != .symbver) {
                    version_lvalue(ir.dest.?, bb, ir, &bb.parameters);
                }
                if (ir.src1 != null) {
                    version_lvalue(ir.src1.?, bb, ir, &bb.parameters);
                }
                if (ir.src2 != null) {
                    version_lvalue(ir.src2.?, bb, ir, &bb.parameters);
                }

                if (ir.data == .lval_list) {
                    for (ir.data.lval_list.items) |lval| {
                        version_lvalue(lval, bb, ir, &bb.parameters);
                    }
                }
            }

            if (bb.has_branch) {
                version_lvalue(bb.condition.?, bb, null, &bb.parameters);
            }
        }

        self.clearVisitedBBs();
        var i: usize = 0;
        while (self.childrenArgPropagation(self.block_graph_head orelse return, allocator)) {
            self.clearVisitedBBs();
            i += 1;
        }
        self.clearVisitedBBs();
    }

    fn version_lvalue(lval: *lval_.L_Value, bb: *basic_block_.Basic_Block, ir: ?*ir_.IR, parameters: *std.ArrayList(*lval_.Symbol_Version)) void {
        switch (lval.*) {
            .symbver => {
                var retval = lval.symbver.findVersion(bb.ir_head, ir);
                if (retval.version == null) {
                    _ = retval.putSymbolVersionSet(parameters);
                }
                lval.symbver = retval;
            },
            .dereference => version_lvalue(lval.dereference.expr, bb, ir, parameters),
            .index => {
                version_lvalue(lval.index.rhs, bb, ir, parameters);
                version_lvalue(lval.index.lhs, bb, ir, parameters);
                if (lval.index.upper_bound != null) {
                    version_lvalue(lval.index.upper_bound.?, bb, ir, parameters);
                }
            },
            .select => {
                version_lvalue(lval.select.lhs, bb, ir, parameters);
                if (lval.select.tag != null) {
                    version_lvalue(lval.select.tag.?, bb, ir, parameters);
                }
            },
        }
    }
};
