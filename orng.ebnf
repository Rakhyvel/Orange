# https://github.com/proebsting/rdgen
# used just to check for ambiguity
# python3.10 ../rdgen/main.py create --input orng.ebnf --output parser.py && grep "AMBIGUOUS" parser.py

program: top_level_declaration {"newline" top_level_declaration} "eof".

top_level_declaration: fn_declaration 
                     | const_declaration
                     .

non_fn_declaration: let_declaration | const_declaration.

const_declaration: "const" ident [":" arrowExpr] "=" expr.

let_declaration: "let" ["mut"] ident (":" arrowExpr ["=" expr] | "=" expr).

fn_declaration: "fn" [ident ":"] paramlist "->" arrowExpr ["where" arrowExpr] "=" expr.
paramlist: "(" [param {"," param}] ")".
param: ["mut" | "const"] ident ":" arrowExpr ["=" arrowExpr]. # cannot be productExpr, otherwise ambiguous with ,

statement: non_fn_declaration
         | fn_declaration
         | "defer" statement
         | "errdefer" statement
         | condMatchExpr
         #| arrowExpr [("=" | "+=" | "-=" | "*=" | "/=" | "%=" | "**=") expr]
         .

expr: condMatchExpr
    | fn_declaration
    | sumExpr
    .

sumExpr: productExpr {"|" ident [":" productExpr]}. # (lhs of | must be ident or annotExpr with proper pattern left of the :)

productExpr: annotExpr {"," annotExpr}.

annotExpr: arrowExpr [":" arrowExpr ["where" arrowExpr] ["=" arrowExpr]]. # (lhs of : must be ident or other TBD patterns)

assignExpr: arrowExpr [("=" | "+=" | "-=" | "*=" | "/=" | "%=" | "**=") expr].

arrowExpr: boolExpr {"->" boolExpr}.

boolExpr: neqExpr {("and" | "or") neqExpr}.

neqExpr: conditionalExpr ["!=" conditionalExpr].

conditionalExpr: deltaExpr {("==" | "<" | "<=" | ">" | ">=") deltaExpr}.

deltaExpr: coalesceExpr {">>" coalesceExpr}. # (rhs must be a call expr)

coalesceExpr: intExpr {("orelse" | "catch") intExpr}.

intExpr: termExpr {("+" | "-" | "!") termExpr}.

termExpr: prefixExpr {("*" | "/" | "%" | "<>" | "++" | "--" | "||") prefixExpr}.

prefixExpr: "!" hyperExpr                           # inferred error union
          | "not" hyperExpr                         # boolean not
          | "-" hyperExpr                           # negation
          | "&" ["mut"] prefixExpr                  # address-of
          | "[" ["mut" | expr | "*"] "]" prefixExpr # slice-of/tuple-of/multi-pointer
          | "?" hyperExpr                           # optional
          | "." ident [parens]                      # infered member with optional init
          | hyperExpr
          .

hyperExpr: postfixExpr {(".>" | "**") postfixExpr}.

postfixExpr: factor {parens | ("[" ((".."[expr]) | (expr[".." [expr]])) "]") | ("." ident) | (".?") | (".!") | ("^")}.

factor: ident
      | "true"
      | "false"
      | int
      | hex
      | octal
      | bin
      | float
      | char
      | string
      | blockExpr
      | ifExpr
      | whileExpr
      | forExpr
      | "unreachable"
      | parens
      .

indentBlockExpr: indent [statement {"newline" statement }] [("throw" expr "newline") | ("return" [expr] "newline") | ("continue" "newline") | ("break" "newline")] dedent.
braceBlockExpr: "{" {statement ";"} [("throw" expr) | ("return" [expr]) | ("continue") | ("break")] "}".
blockExpr: indentBlockExpr | braceBlockExpr.

ifExpr: "if" [non_fn_declaration ";"] expr blockExpr ["else" blockExpr].

whileExpr: "while" [non_fn_declaration ";"] expr [";" statement ] blockExpr ["else" blockExpr].

forExpr: "for" ["mut"] ident "in" expr blockExpr ["else" blockExpr].

barClause: annotExpr ["=>" annotExpr]. # for barClauses in match statements, if the lhs of has a pattern, => must be present
barElse: "else" "=>" annotExpr.
barListMiddle: barClause ["|" (barElse | barListMiddle)].
barList: "|" barListMiddle.

condExpr: [non_fn_declaration ";"] barList.
matchExpr: [non_fn_declaration ";"] productExpr barList.
condMatchExpr: "cond" condExpr
             | "match" matchExpr
             .

parens: "(" expr ")". # ABSOLUTELY needs to be `"(" expr ")"` so that you can put exprs in parens